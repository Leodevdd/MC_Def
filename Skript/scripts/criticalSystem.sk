on join:
	if {critchance.%player%} is not set:
		set {critchance.%player%} to 1 # default crit chance.



# 치명타 관리
on damage:
	# 피격 지연 상태 예외처리
	if {damageDelay.%attacker's uuid%::%victim's uuid%} is true:
		stop trigger


	if event-damagecause is projectile:
		if hit is critical:
			set damage to damage # 활 데미지 공식을 더 알아볼 필요가 있음. 값이 6 ~ 10.9로 튐
			play sound "entity.player.attack.crit" with volume 1 at victim
			play mob spawner flames at victim
		stop trigger

	# force damage 구문으로부터 on damage 이벤트 재호출 방지
	if {prevAttack.%attacker%} is true:
		set {prevAttack.%attacker%} to false
		stop trigger

	# 치명타인 경우: cancel event
	# why? -> 직접 customize 위해 치명타 이펙트 및 사운드 제거
	if hit is critical:
		cancel event
		set {prevAttack.%attacker%} to true

		# 치명타->치명타
		if chance of {critChance.%attacker%}%:
			# damage * 2 = on damage문에서 damage가 절반으로 나옴. 이유는 모름.
			# / 3 * 2 = 치명타 피해량(3/3) = 기본 피해량(2/3)
			force attacker to damage victim by damage*2/3*2*2
			play sound "entity.player.attack.crit" with volume 1 at victim
			play mob spawner flames at victim
		# 치명타->일반 공격
		else:
			force attacker to damage victim by damage*2/3*2

	# 일반 공격->치명타
	else if chance of {critChance.%attacker%}%:
		set damage to damage * 2
		play sound "entity.player.attack.crit" with volume 1 at victim
		play mob spawner flames at victim

on shoot:
	# 화살 치명타 무효화 (최대로 당긴 활은 무조건 치명타 -> 초기화)
	if projectile critical mode of event-projectile is true:
		set projectile critical mode of event-projectile to false

	# 확률 기반 치명타 시스템 적용
	if chance of {critChance.%shooter%}%:
		set projectile critical mode of event-projectile to true



	